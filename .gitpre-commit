#!/usr/bin/env perl
# @(#)Ident: .gitpre-commit 2013-04-02 18:27 pjf ;

use strict;
use warnings;
use version; our $VERSION = qv( sprintf '0.1.%d', q$Rev: 4 $ =~ /\d+/gmx );

use English               qw(-no_match_vars);
use File::Spec::Functions qw(catfile updir);

my $file = -f 'Build.PL'    ? 'Build.PL'    :
           -f 'Makefile.PL' ? 'Makefile.PL' : undef;
my $rev;

if (defined $file) {
   open my $in, '<', $file or die 'File ${file} cannot open: ${OS_ERROR}';

   my $content  = do { local $RS = undef; <$in> }; close $in;
   my $module   = (map    { eval $_ }
                   map    { m{ \A \s* (?:module|name) \s* => \s* (.+) [,] }imx }
                   grep   { m{ \A \s*   (module|name) }imx }
                   split m{ [\n] }mx, $content)[ 0 ];
   my $distname = (map { s{ :: }{-}gmx; lc $_ } $module)[ 0 ];

   $file = catfile( updir, ".${distname}.rev" );

   if (-f $file) {
      open $in, '<', $file or die "File ${file} cannot open: ${OS_ERROR}";
      $rev = do { local $RS = undef; <$in> }; close $in; chomp $rev; $rev++;
   }
}

if (defined $rev and qx{ git branch 2>/dev/null } =~ m{ \A [*] \s+ master }mx) {
   my @stat = split m{ \n }mx, qx{ git status --porcelain }; my $found = 0;

   for my $path (map { s{ \A .+ \s+ }{}mx; $_ } grep { m{ \A [AM] }mx } @stat) {
      open my $in,  '<', $path or die "Path ${path} cannot open: ${OS_ERROR}";

      my $content = do { local $RS = undef; <$in> }; close $in;

      $content =~ m{ \$ (Rev (?:ision)?) (?:[:] \s+ (\d+) \s+)? \$ }mx or next;
      $content =~ s{ \$ (Rev (?:ision)?) (?:[:] \s+ (\d+) \s+)? \$ }{\$$1\$}gmx;

      $rev < ($2 || 0) + 1 and $rev = ($2 || 0) + 1;

      $content =~ s{ \$ (Rev (?:ision)?) \$ }{\$$1: $rev \$}gmx;
      open my $out, '>', $path or die "Path ${path} cannot open: ${OS_ERROR}";
      print {$out} $content; close $out;

      my $cmd = "git add ${path}"; qx{ $cmd }; $found = 1;
   }

   if ($found) {
      open my $out, '>', $file or die "File ${file} cannot open: ${OS_ERROR}";
      print ${out} "${rev}\n"; close $out;
   }
}

exit 0;
